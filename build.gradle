apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'jacoco'

repositories {
	mavenCentral()
}

sourceCompatibility = targetCompatibility = '1.6'

tasks.withType(JavaCompile) {
	options.encoding = 'CP1252'
}

configurations {
  ajc
}

dependencies {
	compile "net.java.dev.stax-utils:stax-utils:20070216"
	compile 'commons-collections:commons-collections:3.2'
	compile 'commons-lang:commons-lang:2.3'
	compile 'commons-logging:commons-logging:1.1'
	compile 'log4j:log4j:1.2.13'
	compile 'org.aspectj:aspectjrt:1.6.9'
	compile files('src/lib/joal.jar')

	// AspectJ dependency used for compiling only
	ajc 'org.aspectj:aspectjtools:1.6.9'

	testCompile 'junit:junit:4.7'
}

compileJava {
	doLast {
		// Configuration to weave the aspects
		ant.taskdef( resource:"org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties", classpath: configurations.ajc.asPath)
		ant.iajc(source:"1.6", target:"1.6", destDir:sourceSets.main.output.classesDir.absolutePath, maxmem:"512m", fork:"true",
			sourceRootCopyFilter:"**/*.java",classpath:configurations.compile.asPath){

 			sourceroots{
				sourceSets.main.java.srcDirs.each{
					pathelement(location:it.absolutePath)
				}
			}
		}
	}
}

test {
	ignoreFailures = true
}

check {
	dependsOn jacocoTestReport
}

task wrapper(type: Wrapper) {
	gradleVersion = '1.8'
}

build {
	dependsOn javadoc
}
